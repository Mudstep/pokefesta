ENTRY(Start)

gNumMusicPlayers = 4;
gMaxLines = 60;

SECTIONS {
    . = 0x2020000;

    ewram (NOLOAD) :
    ALIGN(4)
    {
        src/main.o(ewram_data);
        src/sprite.o(ewram_data);
        src/text.o(ewram_data);
        src/string_util.o(ewram_data);
        . += 0x2E8; /* big gap */
        src/link.o(ewram_data);
        src/rtc.o(ewram_data);
    }

    . = 0x3000000;

    iwram (NOLOAD) :
    ALIGN(4)
    {
        src/main.o(.bss);
        src/sprite.o(.bss);
        src/text.o(.bss);
        src/string_util.o(.bss);
        src/link.o(.bss);
        src/rtc.o(.bss);

        . = 0xF28;
        src/agb_flash.o(.bss);
        . = 0xF36;
        src/siirtc.o(.bss);
        tools/agbcc/lib/libgcc.a:dp-bit.o(.bss);
        tools/agbcc/lib/libgcc.a:fp-bit.o(.bss);

        src/m4a_2.o(.bss.code);

        src/main.o(iwram_data);
        src/sprite.o(iwram_data);
        src/text.o(iwram_data);
        src/string_util.o(iwram_data);
        src/link.o(iwram_data);
        src/rtc.o(iwram_data);

        . = 0x5FD0;
        src/m4a_2.o(iwram_data);

        . = 0x7490;
        src/agb_flash.o(iwram_data);
    }

    . = 0x8000000;

    .text :
    ALIGN(4)
    {
        asm/crt0.o(.text);
        src/main.o(.text);
        src/sprite.o(.text);
        src/text.o(.text);
        src/string_util.o(.text);
        src/link.o(.text);
        src/rtc.o(.text);
        asm/rom3.o(.text);
        src/play_time.o(.text);
        asm/rom4.o(.text);
        src/palette.o(.text);
        asm/rom_8074BAC.o(.text);
        src/task.o(.text);
        asm/rom5.o(.text);
    } =0

    script_data :
    ALIGN(4)
    {
        data/data1.o(script_data);
    } =0

    lib_text :
    ALIGN(4)
    {
        asm/libgcnmultiboot.o(.text);
        asm/m4a_1.o(.text);
        src/m4a_2.o(.text);
        asm/m4a_3.o(.text);
        src/m4a_4.o(.text);
        src/agb_flash.o(.text);
        src/agb_flash_1m.o(.text);
        src/agb_flash_mx.o(.text);
        src/siirtc.o(.text);
        asm/libagbsyscall.o(.text);
        tools/agbcc/lib/libgcc.a:_call_via_rX.o(.text);
        tools/agbcc/lib/libgcc.a:_divsi3.o(.text);
        tools/agbcc/lib/libgcc.a:_dvmd_tls.o(.text);
        tools/agbcc/lib/libgcc.a:_fixunsdfsi.o(.text);
        tools/agbcc/lib/libgcc.a:_fixunssfsi.o(.text);
        tools/agbcc/lib/libgcc.a:_modsi3.o(.text);
        tools/agbcc/lib/libgcc.a:_muldi3.o(.text);
        tools/agbcc/lib/libgcc.a:_udivdi3.o(.text);
        tools/agbcc/lib/libgcc.a:_udivsi3.o(.text);
        tools/agbcc/lib/libgcc.a:_umodsi3.o(.text);
        tools/agbcc/lib/libgcc.a:dp-bit.o(.text);
        tools/agbcc/lib/libgcc.a:fp-bit.o(.text);
        tools/agbcc/lib/libgcc.a:_lshrdi3.o(.text);
        tools/agbcc/lib/libgcc.a:_negdi2.o(.text);
        asm/libc.o(.text);
    } =0

    .rodata :
    ALIGN(4)
    {
        src/main.o(.rodata);
        src/sprite.o(.rodata);
        . += 0x8; /* Why the gap? */
        src/text.o(.rodata);
        src/string_util.o(.rodata);
        src/link.o(.rodata);
        src/rtc.o(.rodata);
        data/data2.o(.rodata);
        src/m4a_tables.o(.rodata);
        data/sound_data.o(.rodata);
        src/agb_flash.o(.rodata);
        src/agb_flash_1m.o(.rodata);
        src/agb_flash_mx.o(.rodata);
        src/agb_flash_le.o(.rodata);
        src/siirtc.o(.rodata);
        tools/agbcc/lib/libgcc.a:_udivdi3.o(.rodata);
    } =0

    . = 0x8D00000;

    gfx_data :
    ALIGN(4)
    {
        data/graphics.o(gfx_data);
    } =0

    /* Discard everything not specifically mentioned above. */
    /DISCARD/ :
    {
        *(*);
    }
}

BEGIN{

	#=================================================================
	#
	#アイテム定義ファイル読み込み
	#
	#=================================================================
	ITEMSYM_H = "../data/itemsym.h"

	#getline = １行読み込み
	#１は戻り値
	while ( 1 == getline < ITEMSYM_H ) {
		if ( NF == 5 && $1 =="#define" ) {
			ItemName[$5] = $2
		}
	}
	close(ITEMSYM_H)

	#====================================================================
	#
	#フィールド上のモンスターボール
	#
	#====================================================================
	printf( "//フィールド上のアイテムのラベル \n" )
	print ""
	print ""
}


NF >= 6{

	#-----------------------------------------------------------------
	#
	#同じ番号(フラグナンバーのオフセット)を使用してないかチェック
	#
	#-----------------------------------------------------------------
	if ( $7 in ID_List ) {

		#同じ数値を定義している個所がある場合
		printf("同じ値の定義があります\n") >> "/dev/stderr"
		printf("[%s] [%s] %s\n",ID_List[$7],$1,$7) >> "/dev/stderr"
		printf("ERROR! %s\n",$1)

		exit				#途中で終了
	}

	ID_List[$7] = $1		#要素として登録
	#-----------------------------------------------------------------


	if( $1 == "マップ名" )	next
	if( $1 ~ "アイテム" )	next


	#フラグマックスを超えていたらエラー(02/08/15 MAX = 199 )
	if( $7 > 199 ) 
	{
		printf("フラグの最大数を超えています！\n") >> "/dev/stderr"
		exit				#途中で終了
	}

	if( NF > 4 )
	{
		#if( $1 != "マップ名" || $1 != "アイテム" )	next

		print "//" $0
		print "SCRIPT_LABEL		ev_ball1_" $7
		print "\t_FLD_ITEM_EVENT	" ItemName[$6] ",1"
		print "\t_END"
		print ""
	}

}



